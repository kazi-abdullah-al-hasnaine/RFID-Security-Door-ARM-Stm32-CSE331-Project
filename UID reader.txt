#include <SPI.h>
#include <MFRC522.h>
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

#define RST_PIN     PB0    // RC522 RST
#define SS_PIN      PA4    // RC522 NSS (SDA)
#define LED_PIN     PC13   // Onboard LED (active LOW)

#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
#define OLED_RESET    -1

Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);
MFRC522 mfrc522(SS_PIN, RST_PIN);   // Create MFRC522 instance

void setup() {
  pinMode(LED_PIN, OUTPUT);
  digitalWrite(LED_PIN, HIGH);  // LED off initially

  Serial.begin(9600);

  SPI.begin();
  mfrc522.PCD_Init(); // Init RC522
  delay(50);

  Wire.begin();
  if (!display.begin(SSD1306_SWITCHCAPVCC, 0x3C)) {
    while (1); // OLED init failed
  }

  display.clearDisplay();
  display.setTextSize(1);
  display.setTextColor(WHITE);
  display.setCursor(0, 0);
  display.println("RFID + OLED Ready");
  display.display();
  delay(1000);
}

void loop() {
  display.clearDisplay();
  display.setCursor(0, 0);
  display.println("Waiting for card...");
  display.display();

  if (!mfrc522.PICC_IsNewCardPresent() || !mfrc522.PICC_ReadCardSerial()) {
    delay(500);
    return;
  }

  // Card detected
  display.clearDisplay();
  display.setCursor(0, 0);
  display.println("Card detected!");
  display.setCursor(0, 15);
  display.print("UID: ");

  Serial.print("UID: ");
  for (byte i = 0; i < mfrc522.uid.size; i++) {
    byte val = mfrc522.uid.uidByte[i];
    if (val < 0x10) {
      display.print("0");
      Serial.print("0");
    }
    display.print(val, HEX);
    display.print(" ");
    Serial.print(val, HEX);
    Serial.print(" ");
  }
  display.display();
  Serial.println();

  blinkLED(100);

  mfrc522.PICC_HaltA();      
  mfrc522.PCD_StopCrypto1(); 

  delay(1500);
}

void blinkLED(int duration) {
  digitalWrite(LED_PIN, LOW);  // LED ON
  delay(duration);
  digitalWrite(LED_PIN, HIGH); // LED OFF
}